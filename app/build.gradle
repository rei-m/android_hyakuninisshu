apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    signingConfigs {
        releaseConfig
    }

    if (project.hasProperty('storeFile')) {
        android.signingConfigs.releaseConfig.storeFile = file(storeFile)
    }
    if (project.hasProperty('storePassword')) {
        android.signingConfigs.releaseConfig.storePassword = storePassword
    }
    if (project.hasProperty('keyAlias')) {
        android.signingConfigs.releaseConfig.keyAlias = keyAlias
    }
    if (project.hasProperty('keyPassword')) {
        android.signingConfigs.releaseConfig.keyPassword = keyPassword
    }

    compileSdkVersion 27
    buildToolsVersion '27.0.0'
    defaultConfig {
        applicationId "me.rei_m.hyakuninisshu"
        minSdkVersion 16
        targetSdkVersion 27
        multiDexEnabled true
        versionCode 11
        versionName "1.0.10"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "d"
        }
        release {
            signingConfig signingConfigs.releaseConfig
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    ext.android_support_lib_version = '27.0.0'
    ext.okhttp_version = '3.4.1'
    ext.retrofit_version = '2.1.0'
    ext.streamsupport_version = '1.5.2'
    ext.rxjava_version = '2.1.4'
    ext.dagger_version = '2.11'
    ext.leakcanary_version = '1.5.4'
    ext.play_services_version = '11.4.2'

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:support-annotations:$android_support_lib_version"
    implementation "com.android.support:appcompat-v7:$android_support_lib_version"
    implementation "com.android.support:support-v4:$android_support_lib_version"
    implementation "com.android.support:percent:$android_support_lib_version"
    implementation "com.android.support:design:$android_support_lib_version"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:multidex:1.0.2'
    implementation "com.google.firebase:firebase-core:$play_services_version"
    implementation "com.google.android.gms:play-services-ads:$play_services_version"

    compileOnly 'javax.annotation:jsr250-api:1.0'
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"

    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    implementation 'com.squareup.moshi:moshi:1.5.0'

    implementation 'com.github.bumptech.glide:glide:4.2.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.2.0'

    implementation 'com.github.hotchemi:android-rate:1.0.1'

    annotationProcessor 'com.github.gfx.android.orma:orma-processor:4.2.5'
    implementation 'com.github.gfx.android.orma:orma:4.2.5'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.4.2'
    testImplementation 'org.mockito:mockito-core:2.10.0'
    testImplementation('com.android.support.test:runner:1.0.1') {
        exclude module: 'support-annotations'
    }
    testImplementation('com.android.support.test:rules:1.0.1') {
        exclude module: 'support-annotations'
    }

    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1') {
        exclude module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    }
    androidTestImplementation('com.android.support.test.espresso:espresso-intents:3.0.1') {
        exclude module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    }
    androidTestImplementation('com.android.support.test:runner:1.0.1') {
        exclude module: 'support-annotations'
    }
    androidTestImplementation('com.android.support.test:rules:1.0.1') {
        exclude module: 'support-annotations'
    }
    androidTestImplementation 'org.mockito:mockito-core:2.10.0'
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    androidTestImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"

    implementation('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true
    }
}

apply plugin: 'com.google.gms.google-services'